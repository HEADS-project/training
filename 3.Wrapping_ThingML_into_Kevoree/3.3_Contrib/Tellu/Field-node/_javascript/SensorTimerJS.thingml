import "../SensorTimer.thingml"

// Manage a set of software timers.
thing SensorTimerJS includes SensorTimer
@pim "SensorTimer"
@platform "javascript"
@javascript "node browser"
{
    function cancel() 
    do
        'clearTimeout(this.timer);'
    end
    
    function starting(delay : Integer)
    do
        'this.timer = setTimeout(onTimeout,' & delay & ');'
    end
    
    function startInterval(interval : Integer)
    do
        'this.timer = setInterval(onTimeout,' & interval & ');'
    end
    
    function onTimeout() 
    do
        timer!timer_timeout()
    end
    
    statechart SoftTimer init default {
        state default {
            
          internal startState
            event m : timer?timer_start
            guard m.delay > 0
            action do
                starting(m.delay)
            end
            
           internal startInterval
            event m : timer?timer_start_interval
            guard m.interval > 0
            action do
                startInterval(m.interval)
            end
            
          internal cancel
            event m : timer?timer_cancel
            action cancel()    
        }
    }
}

