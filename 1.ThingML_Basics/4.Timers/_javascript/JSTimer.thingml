import "../Timer.thingml"

// Manage a set of software timers.
thing TimerJS includes Timer
@pim "Timer"
@platform "javascript"
@javascript "node browser"
{
    function cancel() 
    do
        'clearTimeout(this.timer);'
    end
    
    function _start(delay : Integer)
    do        
        'this.timer = setTimeout(()=>{' 
        	timer!timer_timeout() 
        '},' & delay & ');'        
    end
       
    statechart SoftTimer init default {
        state default {
            
          internal _start
            event m : timer?timer_start
            guard m.delay > 0
            action do
                _start(m.delay)
            end
            
          internal cancel
            event m : timer?timer_cancel
            action cancel()    
        }
    }
}

configuration TestTimerJS {
    instance timer : TimerJS
    instance client : HelloTimer//this is your application
    connector client.timer => timer.timer    
}
